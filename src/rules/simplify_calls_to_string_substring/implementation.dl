/* Pre  [:x].substring(0) */
/* Post [:x]              */
rewrite(2121, filename, start, end, subject_str) :-
    method_invocation(id, subject, "substring", [arg, nil]),
        filename_of(id, filename),
        starts_at(id, start),
        ends_at(id, end),
    has_string_type(subject),
    string_representation(arg, "0"),
    string_representation(subject, subject_str).

/* Pre  [:x].substring(0, [:x].length()) */
/* Post [:x]                             */
rewrite(2121, filename, start, end, subject_str) :-
    method_invocation(id, subject, "substring", [arg1, [arg2, nil]]),
        filename_of(id, filename),
        starts_at(id, start),
        ends_at(id, end),
    has_string_type(subject),
    string_representation(subject, subject_str),
    string_representation(arg1, "0"),
    string_representation(arg2, cat(subject_str, ".length()")).

/* Pre  [:x].substring([:x].length()) */
/* Post ""                         */
rewrite(2121, filename, start, end, "\"\"") :-
    method_invocation(id, subject, "substring", [arg, nil]),
        filename_of(id, filename),
        starts_at(id, start),
        ends_at(id, end),
    has_string_type(subject),
    string_representation(subject, subject_str),
    string_representation(arg, cat(subject_str, ".length()")).

/* Pre  [:x].substring([:y], [:x].length()) */
/* Post [:x].substring([:y])                */
rewrite(2121, filename, start, end, cat(subject_str, ".substring(", arg1_str, ")")) :-
    method_invocation(id, subject, "substring", [arg1, [arg2, nil]]),
        filename_of(id, filename),
        starts_at(id, start),
        ends_at(id, end),
    has_string_type(subject),
    ! string_representation(arg1, "0"),
    string_representation(subject, subject_str),
    string_representation(arg2, cat(subject_str, ".length()")),
    string_representation(arg1, arg1_str).
