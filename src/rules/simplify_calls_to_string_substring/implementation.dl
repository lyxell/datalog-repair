/* Pre  [:x].substring(0) */
/* Post [:x]              */
rewrite_node("simplify_calls_to_string_substring", id, subject_str) :-
    method_invocation(id, subject, "substring", [arg, nil]),
    has_string_type(subject),
    string_representation(arg, "0"),
    string_representation(subject, subject_str).

/* Pre  [:x].substring(0, [:x].length()) */
/* Post [:x]                             */
rewrite_node("simplify_calls_to_string_substring", id, subject_str) :-
    method_invocation(id, subject, "substring", [arg1, [arg2, nil]]),
    has_string_type(subject),
    string_representation(subject, subject_str),
    string_representation(arg1, "0"),
    string_representation(arg2, cat(subject_str, ".length()")).

/* Pre  [:x].substring([:x].length()) */
/* Post ""                         */
rewrite_node("simplify_calls_to_string_substring", id, "\"\"") :-
    method_invocation(id, subject, "substring", [arg, nil]),
    has_string_type(subject),
    string_representation(subject, subject_str),
    string_representation(arg, cat(subject_str, ".length()")).

/* Pre  [:x].substring([:y], [:x].length()) */
/* Post [:x].substring([:y])                */
rewrite_node("simplify_calls_to_string_substring", id, cat(subject_str, ".substring(", arg1_str, ")")) :-
    method_invocation(id, subject, "substring", [arg1, [arg2, nil]]),
    has_string_type(subject),
    ! string_representation(arg1, "0"),
    string_representation(subject, subject_str),
    string_representation(arg2, cat(subject_str, ".length()")),
    string_representation(arg1, arg1_str).
