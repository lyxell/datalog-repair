/**
 * Identical expressions should not be used on both sides of a binary operator
 * https://rules.sonarsource.com/java/RSPEC-1764
 */

/* Pre  [:x] == [:x] */
/* Post true         */
rewrite(1764, filename, id, "true") :-
    equals_expression(id, left, right),
    filename_of(id, filename),
        /* allow x == x as a check for NaN, see
           https://rules.sonarsource.com/java/RSPEC-1764 */
        ! has_floating_point_type(left),
    string_representation(left, str),
    string_representation(right, str).

/* Pre  [:x] != [:x] */
/* Post false        */
rewrite(1764, filename, id, "false") :-
    not_equals_expression(id, left, right),
    filename_of(id, filename),
    string_representation(left, str),
    string_representation(right, str).

/* Pre  [:x] && [:x] */
/* Post [:x]         */
rewrite(1764, filename, id, str) :-
    conditional_and_expression(id, left, right),
    filename_of(id, filename),
    string_representation(left, str),
    string_representation(right, str).

/* Pre  [:x] || [:x] */
/* Post [:x]         */
rewrite(1764, filename, id, str) :-
    conditional_or_expression(id, left, right),
    filename_of(id, filename),
    string_representation(left, str),
    string_representation(right, str).

/* Pre  [:x] - [:x] */
/* Post 0           */
rewrite(1764, filename, id, "0") :-
    subtraction_expression(id, left, right),
    filename_of(id, filename),
    string_representation(left, str),
    string_representation(right, str).

/* Pre  [:x] / [:x] */
/* Post 1           */
rewrite(1764, filename, id, "1") :-
    division_expression(id, left, right),
    filename_of(id, filename),
    string_representation(left, str),
    string_representation(right, str).
