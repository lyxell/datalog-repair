/* https://rules.sonarsource.com/java/RSPEC-2095 */

/*
    TODO: Instances of the following classes should be ignored by this rule because close has no effect:

    java.io.ByteArrayOutputStream
    java.io.ByteArrayInputStream
    java.io.CharArrayReader
    java.io.CharArrayWriter
    java.io.StringReader
    java.io.StringWriter
 */

/**
 * Rewrite a try-catch statement into a try-with-resources statement.
 */
rewrite(2095, filename, id, cat("try (", declaration_str, ") {", first_frag, second_frag)) :-

    /**
     * We are inside a try statement
     *
     * try {
     */
    try_statement(id, body, _, _),
    filename_of(id, filename),
    block(body, statements),

    /**
     * The first statement of the try body is a local variable declaration that
     * declares exactly one resource
     *
     * BufferedWriter bw = new BufferedWriter(new FileWriter(file));
     */
    statements = [declaration_statement, _],
    local_variable_declaration_statement(declaration_statement, declaration),
    local_variable_declaration(declaration, _, type, [_, nil]),
    is_autocloseable_type(type),

    /**
     * The try body also contains a method invocation, closing the resource
     *
     * bw.close();
     */
    list_contains(statements, expr_statement),
    expression_statement(expr_statement, expr),
    method_invocation(expr, subject, method, nil),
    point_of_declaration(subject, declaration),
    string_representation(method, "close"),

    /**
     * Output
     */
    source_code(filename, code),
    string_representation(declaration, declaration_str),

    /**
     * The first source code fragment starts at the end of the declaration
     * statement and ends at the end of the statement preceding the method
     * invocation
     */
    ends_at(declaration_statement, first_frag_start),
    predecessor_of(predecessor, expr_statement),
    ends_at(predecessor, first_frag_end),
    first_frag = substr(code, first_frag_start, first_frag_end - first_frag_start),

    /**
     * The second source code fragment starts at the end of the method invoation
       and ends at the end of the try-catch statement
     */
    ends_at(expr_statement, second_frag_start),
    ends_at(id, second_frag_end),
    second_frag = substr(code, second_frag_start, second_frag_end - second_frag_start).

/**
 * Rewrite
 * 
 * {
 *     ... first fragment ...
 *     Resource resource = new Resource();
 *     ... second fragment ...
 *     resource.close();
 *     ... third fragment ...
 * }
 * 
 * into
 *
 * {
 *     ... first fragment ...
 *     try (Resource resource = new Resource()) {
 *         ... second fragment ...
 *     }
 *     ... third fragment ...
 * }
 */
