/* https://rules.sonarsource.com/java/RSPEC-2095 */

/*
    Instances of the following classes are ignored by this rule because close has no effect:

    java.io.ByteArrayOutputStream
    java.io.ByteArrayInputStream
    java.io.CharArrayReader
    java.io.CharArrayWriter
    java.io.StringReader
    java.io.StringWriter
 */


/**
 * Rewrite
 *
 * try {
 *     Resource resource = new Resource();
 *     ... first fragment ...
 *     resource.close();
 *     ... second fragment ...
 *
 * into
 *
 * try (Resource resource = new Resource()) {
 *     ... first fragment ...
 *     ... second fragment ...
 *
 */

rewrite(2095, "", filename, id, cat("try (", declaration_str, ") {", first_frag, second_frag)) :-

    /* we are inside a try statement */
    try_statement(id, body, _, _),
    filename_of(id, filename),
    block(body, statements),

    /* the first statement of the try body is a local variable declaration that declares exactly one resource */
    statements = [declaration_statement, _],
    local_variable_declaration_statement(declaration_statement, declaration),
    local_variable_declaration(declaration, _, type, [_, nil]),
    is_autocloseable_type(type),

    /* the try body also contains a method invocation, closing the resource */
    list_contains(statements, expr_statement),
    expression_statement(expr_statement, expr),
    method_invocation(expr, subject, method, nil),
    point_of_declaration(subject, declaration),
    string_representation(method, "close"),

    /* output */
    source_code(filename, code),
    string_representation(declaration, declaration_str),

    /* first fragment starts at the end of the declaration statement and ends
     * at the end of the statement preceding the method invocation */
    predecessor_of(predecessor, expr_statement),
    ends_at(declaration_statement, first_frag_start),
    ends_at(predecessor, first_frag_end),
    first_frag = substr(code, first_frag_start, first_frag_end - first_frag_start),

    /* second fragment starts at the end of the statement of the method
     * invocation and spans the rest of the original try-catch */
    ends_at(expr_statement, second_frag_start),
    ends_at(id, second_frag_end),
    second_frag = substr(code, second_frag_start, second_frag_end - second_frag_start).

/**
 * Rewrite
 * 
 * {
 *     ... first fragment ...
 *     Resource resource = new Resource();
 *     ... second fragment ...
 *     resource.close();
 *     ... third fragment ...
 * }
 * 
 * into
 *
 * {
 *     ... first fragment ...
 *     try (Resource resource = new Resource()) {
 *         ... second fragment ...
 *     }
 *     ... third fragment ...
 * }
 */
