/**
 * The diamond operator ("<>") should be used
 *
 * https://rules.sonarsource.com/java/type/Code%20Smell/RSPEC-2293
 */

#define MESSAGE "Replace the type specification in this constructor call with the diamond operator (<>)"

.decl is_in_context_where_type_is_inferred(id: id)
is_in_context_where_type_is_inferred(id) :-
    return_statement(_, id).
is_in_context_where_type_is_inferred(id) :-
    assignment_expression(_, _, id).
is_in_context_where_type_is_inferred(id) :-
    variable_declarator(_, _, id).
is_in_context_where_type_is_inferred(id) :-
    variable_declarator(_, _, id).
is_in_context_where_type_is_inferred(then),
is_in_context_where_type_is_inferred(else) :-
    ternary_conditional_expression(id, _, then, else),
    is_in_context_where_type_is_inferred(id).

rewrite(2293, MESSAGE, filename, class_type, cat(name_str, "<>")) :-
    filename_of(class_type, filename),
    class_instance_creation_expression(id, nil, nil, class_type, _, _),
    is_in_context_where_type_is_inferred(id),
    parent_of_list(class_type, "name", [name, nil]),
    string_representation(name, name_str),
    parent_of_list(class_type, "type_arguments", type_args),
    type_args != nil.

