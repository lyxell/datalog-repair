.decl field_or_local_variable_declaration(id: id, modifiers: id_list, type: id, declarators: id_list)
field_or_local_variable_declaration(id, mods, type, declarators) :-
    field_declaration(id, mods, type, declarators).
field_or_local_variable_declaration(id, mods, type, declarators) :-
    local_variable_declaration(id, mods, type, declarators).

/* Pre  [:left_type]<[:left_type_parameters]> [:left] = new [:right_type](); */
/* Post [:left_type]<[:left_type_parameters]> [:left] = new [:right_type]<>(); */
rewrite_node("fix_raw_use_of_generic_class", right_type, cat(right_type_str, "<>")) :-
    field_or_local_variable_declaration(_, _, left_type, declarators),
    class_type(left_type, left_type_name, type_args, _),
    type_arguments(type_args, args),
    args != nil,
    list_contains(declarators, declarator),
    variable_declarator(declarator, _, initializer),
    class_instance_creation_expression(initializer, nil, nil, right_type, _, nil),
    class_type(right_type, right_type_name, nil, _),
    string_representation(right_type, right_type_str).
//  collection_type_name(left_type_name),
//  collection_type_name(right_type_name),

/* Pre  Iterator [:var] = [:it]; */
/* Post Iterator<[:type]> [:var] = [:it]; */
rewrite_node("fix_raw_use_of_generic_class", left_type, @type_to_string([package, class, type_args])) :-
    local_variable_declaration(id, _, left_type, [declarator, nil]),
    class_type(left_type, _, nil, _),
    variable_declarator(declarator, _, initializer),
    _has_type(initializer, [package, class, type_args]),
    type_args != nil.
