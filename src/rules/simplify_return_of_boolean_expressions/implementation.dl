.decl does_nothing_but_return_true(id: id)
does_nothing_but_return_true(id) :-
    return_statement(id, ret),
    content_string_representation(ret, "true").
does_nothing_but_return_true(id) :-
    block(id, [statement, nil]),
    does_nothing_but_return_true(statement).

.decl does_nothing_but_return_false(id: id)
does_nothing_but_return_false(id) :-
    return_statement(id, ret),
    content_string_representation(ret, "false").
does_nothing_but_return_false(id) :-
    block(id, [statement, nil]),
    does_nothing_but_return_false(statement).

rewrite_node("simplify_return_of_boolean_expressions", id, cat("return ", cond_str, ";")) :-
    if_statement(id, cond, then, else),
    does_nothing_but_return_true(then),
    does_nothing_but_return_false(else),
    string_representation(cond, cond_str),
    /* id is not the else-branch of an if-statement */
    ! if_statement(_, _, _, id).

rewrite_node("simplify_return_of_boolean_expressions", id, cat("return !(", cond_str, ");")) :-
    if_statement(id, cond, then, else),
    does_nothing_but_return_false(then),
    does_nothing_but_return_true(else),
    string_representation(cond, cond_str),
    /* id is not the else-branch of an if-statement */
    ! if_statement(_, _, _, id).
